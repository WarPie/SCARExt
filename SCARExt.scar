{$L SCARExt.dll}
{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=]
|| What is this: In short I would define SCAREXT as a grabage collection.
|| Alpha v0.1
|| - SLACKY
[=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}
{$I Plotter.scar}

{=-=-=-=-=-=-=-= SCARExt.dll contains the following functions -=-=-=-=-=-=-=-=]
|| Close to every function we got start with `XT_`. So to find am do:
|| Load this include file in SCAR, write `XT_` then press Ctrl+Space ;)
||
|| Plus the plotting lib: TPlot!
[=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}
type
  XT_MatchAlgo = (LAB, LCH, RGB);


{* 
  Convert ATIA to STR (for debugging)...
*}
function ATIAToStr(ATIA: T2DIntArray): String;
var
  i: Integer;
begin
  Result := '[';
  for i:=0 to High(ATIA) do begin
    Result := Result + '('+ TIAToStr(ATIA[i]) + ')';
    if i < High(ATIA) then Result := Result + ', ';
  end;
  Result := Result + ']'
end;  
  
{*
 Copy the client area as bitmap from X->Y2
*}
function XT_ClientBMP(XS,YS,XE,YE: Integer): TSCARBitmap;
begin 
  Result := GetClient.CaptureEx(XS,YS,XE,YE); 
end;


{*
 ....
*}
procedure XT_ClientSize(var Width, Height: Integer);
begin
  GetBoxSize(GetClient.ImageArea, Width, Height);
end;


{*
 Get all the pixels on the bitmap as a Matrix (ImgArray)..
*}
function XT_Bmp2Matrix(bmp: TSCARBitmap; DoFree:Boolean): T2DIntArray;
var
  TIA: TIntArray;
  TPA: TPointArray;
begin  
  TPA := TPAFromBox(Box(0, 0, BMP.Width-1, BMP.Height-1));
  TIA := BMP.GetPixels(TPA);
  Result := XT_TIAToMat(TIA, BMP.Width,  BMP.Height); 
  if DoFree then BMP.Free; 
  SetLengthTIA(TIA, 0);
  SetLengthTPA(TPA, 0);
end;


{*
 Set all the pixels from an Matrix (ImgArray) to the bitmap..
*}
procedure XT_BmpSetData(var bmp: TSCARBitmap; Matrix:T2DIntArray; Adapt:Boolean);
var 
  W,H:Integer;
  TPA:TPointArray;
begin
  W := Length(Matrix[0]); 
  H := Length(Matrix);
  if Adapt then
    if (W > bmp.Width) or (H > bmp.Height) then
      bmp.SetSize(Max(W,bmp.Width),Max(H,bmp.Height));
  TPA := TPAFromBox(Box(0, 0, W-1, H-1));
  bmp.SetPixelsEx(TPA, XT_MatGetValues(Matrix, TPA));
  SetLengthTPA(TPA, 0);
end;


{*
 Set all the pixels from an Matrix (ImgArray) to the bitmap. 
 If the Matrix contains AlphaColor then that pixel/index will not be changed in the bitmap..
*}
procedure XT_BmpSetDataEx(var bmp: TSCARBitmap; Matrix:T2DIntArray; Adapt:Boolean; AlphaColor:Integer);
var 
  W,H:Integer;
  NewW, NewH:Integer; 
  TPA:TPointArray;
  Mat:T2DIntArray;
begin
  W := Length(Matrix[0]); 
  H := Length(Matrix);
  NewW := Max(W, bmp.Width);
  NewH := Max(H, bmp.Height);
  Mat := XT_Bmp2Matrix(bmp, False);
  if Adapt then
    if (NewW <> bmp.Width) or (NewH <> bmp.Height) then
      bmp.SetSize(NewW,NewH);
  TPA := TPAFromBox(Box(0, 0, W-1, H-1)); 
  XT_MatCombine(Matrix, Mat, AlphaColor); 
  bmp.SetPixelsEx(TPA, XT_MatGetValues(Matrix, TPA));
  SetLengthTPA(TPA, 0);
  SetLengthATIA(Mat, 0);
end;


{*
 Loads a bitmap directly in to a matrix.
*}
function XT_LoadBmpToMatrix(Path:String; Ext:String):T2DIntArray;
var 
  Img: TSCARBitmap;
begin
  Img := TSCARBitmap.Create('');
  case Ext of
    'png': if not(Img.LoadFromPng(Path)) then Exit;
    'jpg': if not(Img.LoadFromJpeg(Path)) then Exit;
    'bmp': if not(Img.LoadFromBmp(Path)) then Exit;
    else Exit;
  end;
  Result := XT_Bmp2Matrix(Img, True);
end;


{*
 Saves a Matrix to the computer as a bitmap.
*}
function XT_MatrixSaveAs(Matrix:T2DIntArray; Path:String; Ext:String): Boolean;
var 
  Img: TSCARBitmap;
begin
  Result := False;
  Img := TSCARBitmap.Create('');
  XT_BmpSetData(Img, Matrix, True);
  case Ext of
    'png': if not(Img.SaveToPng(Path)) then Exit;
    'jpg': if not(Img.SaveToJpeg(Path, 100)) then Exit;
    'bmp': if not(Img.SaveToBmp(Path)) then Exit;
    else Exit;
  end;
  Result := True;
  Img.Free;
end;



(*
  Fills the TPA with the found colors. It currently supports two algorithms CIE-LAB, and CIE-LCH.
  For CIE-LCH:
    ColorTol = Hue tolerance (180 = tolmax).
    LightTol = Lightness and Chroma (142 = tolmax).

  For CIE-LAB:
    ColorTol = A and B, together they meassure the Chroma and Hue. (142 = tolmax).
    LightTol = Lightness (100 = tolmax).
    
  For RGB:
    ColorTol = R,G,B tolerance. (255 = tolmax).
    LightTol = Not used.
*)
function XT_FindColorTolEx(var TPA:TPointArray; Color:Integer; Area:TBox; ColorTol, LightTol:Integer; MatchAlgo: XT_MatchAlgo): Boolean;
var 
  W,H:Integer;
  Img:T2DIntArray;
begin
  Result := False;
  XT_ClientSize(W,H);
   
  if (Area.X2 > W) or (Area.X2 <= -1) then Area.X2 := W;
  if (Area.Y2 > H) or (Area.Y2 <= -1) then Area.Y2 := H;
  if (Area.X1 > Area.X2) or (Area.Y1 > Area.Y2) then Exit;
  
  Img := XT_Bmp2Matrix(XT_ClientBMP(Area.X1,Area.Y1,Area.X2,Area.Y2), true); //Results in a bit of overhead. 
  case MatchAlgo of   
    RGB: Result := XT_ImFindColorTolEx(Img, TPA, Color, ColorTol);
    LAB: Result := XT_ImFindColorTolExLAB(Img, TPA, Color, ColorTol, LightTol);
    LCH: Result := XT_ImFindColorTolExLCH(Img, TPA, Color, ColorTol, LightTol);
  end; 
  SetLengthATIA(Img, 0);
  if not(Result) then Exit;
  if (Area.X1=0) and (Area.Y1 = 0) then Exit;
  XT_MoveTPA(TPA, Area.X1, Area.Y1);
end;


{*
 .... Will probably be removed..
*}
function XT_BMPFindEdges(BMP:TSCARBitmap; MinDiff:Integer): TPointArray;
var 
  Img:T2DIntArray;
  W,H:Integer;
begin
  XT_ClientSize(W,H);
  Img := XT_Bmp2Matrix(XT_ClientBMP(0,0,W,H), True);
  Result := XT_ImCEdges(Img, MinDiff);
  SetLengthATIA(Img, 0);
end;